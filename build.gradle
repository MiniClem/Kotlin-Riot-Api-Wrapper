plugins {
    id 'groovy'
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.MiniClem'
version '0.1'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            afterEvaluate {
                artifactId = 'kotlin-api-riot-wrapper'
            }
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Kotlin Riot API Wrapper'
                description = 'A simple to use library to make API call against Riot endpoints on any JVM device'
                url = 'https://github.com/MiniClem/Kotlin-Riot-Api-Wrapper'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'https://github.com/MiniClem/Kotlin-Riot-Api-Wrapper.git'
                    developerConnection = 'https://github.com/MiniClem/Kotlin-Riot-Api-Wrapper.git'
                    url = 'https://github.com/MiniClem/Kotlin-Riot-Api-Wrapper'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Gson
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

//    // Coroutines
//    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}